function(target_tree_sources target)
  set(args ${ARGV})
  list(REMOVE_AT args 0)

  set(expect_base FALSE)
  set(expect_root FALSE)
  set(root "${CMAKE_CURRENT_SOURCE_DIR}")
  set(base)
  set(scope "PRIVATE")
  set(prefix)
  set(sources)

  macro(_flush_target_sources)
    if (sources)
      target_sources("${target}" "${scope}" ${sources})
      source_group(TREE "${root}" PREFIX "${prefix}" FILES ${sources})
      set(sources)
    endif()
  endmacro()

  foreach (arg ${args})
    if (${arg} STREQUAL "ROOT")
      set(expect_root TRUE)
    elseif (expect_root)
      set(root "${arg}")
      set(expect_root FALSE)
    elseif (${arg} STREQUAL "BASE")
      _flush_target_sources()
      set(base)
      set(expect_base TRUE)
      set(scope "PRIVATE")
    elseif (${arg} STREQUAL "PRIVATE" OR ${arg} STREQUAL "PUBLIC" OR ${arg} STREQUAL "INTERFACE")
      if (expect_base)
        message(FATAL_ERROR "Expected base path, got scope modifier ${arg}.")
      endif()
      _flush_target_sources()
      set(scope ${arg})
    elseif (expect_base)
      set(base "${arg}/")
      set(expect_base FALSE)
    else()
      list(APPEND sources "${base}${arg}")
    endif()
  endforeach()

  _flush_target_sources()
endfunction()
